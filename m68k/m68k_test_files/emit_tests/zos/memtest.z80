/*
 * Copyright (c) 1992 Twenty-First Designs, Inc.
 * Unpublished work. All rights reserved.  
 *
 * THIS IS UNPUBLISHED PROPRIETARY SOURCE CODE OF 
 * TWENTY-FIRST DESIGNS, INC.
 *
 * This software source code contains trade secrets and other
 * proprietary information of Twenty-First Designs, Inc. All
 * disclosure or use of this source code is strictly restricted
 * by contract to that required for operation and maintenance 
 * of equipment provided by Twenty-First Designs, Inc.
 * All other disclosure or use of this source code is prohibited.
 *
 ***********************************************************
 *
 * $Id: memtest.z80,v 1.1.1.1 2000/10/18 04:29:56 kent Exp $
 *
 ***********************************************************
 *
 * memtest.a
 * 
 * This module tests the program and data store areas.
 * Read/write tests are used on the RAM, CRC test for the ROM
 *
 ***********************************************************
 */


ROM_LEN	=	8000h		; 27256
RAM_ORG	=	8000h		; start of RAM

	.lcomm	next_location, 2	; cleared locations
	.lcomm	crc_value, 2

	.globl	memtest

memtest:
; check a ROM or RAM location
	ld	a,(ROM_LEN-1)	; last rom location
	inc	a		; 0xff in last word ?
	ret	z		; then no test
	ld	hl,(next_location)
	bit	7,h		; ROM or RAM ?
	jr	z,test_rom
; test RAM -- write data and complement to test
	di			; avoid contention
	ld	a,(hl)
	ld	b,a
	cpl
	ld	(hl),a
	ld	a,(hl)
	ld	(hl),b
	ei
	cpl
	cp	b		; good read ?
	call	nz,bad_error	; bad news if not
; test OK -- next location
	inc	hl
	ld	(next_location),hl
	ret

test_rom:
; take a CRC over all ROM to test
	ld	a,(hl)			; get current data
	inc	hl
	ld	b,h			; save page pointer in B
	ld	(next_location),hl
	ld	hl,(crc_value)
	call	crc			; update crc of next
	ld	(crc_value),hl
	ld	a,#ROM_LEN >> 8		; last location ?
	cp	b
	ret	nz			; not -- so continue
	ld	a,l			; test for zero CRC
	or	h
	ld	hl,#RAM_ORG		; end of ROM -- test RAM next
	ld	(next_location),hl
	ret	z			; passed
	rst	0x38			; bad_error: failed
; NOTREACHED
